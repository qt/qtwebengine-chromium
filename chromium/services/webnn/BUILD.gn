# Copyright 2023 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/fuzzers/mojolpm.gni")

source_set("webnn") {
  sources = [
    "webnn_context_impl.cc",
    "webnn_context_impl.h",
    "webnn_context_provider_impl.cc",
    "webnn_context_provider_impl.h",
    "webnn_graph_impl.cc",
    "webnn_graph_impl.h",
    "webnn_service.cc",
    "webnn_service.h",
  ]

  if (is_win) {
    sources += [
      "dml/adapter.cc",
      "dml/adapter.h",
      "dml/command_queue.cc",
      "dml/command_queue.h",
      "dml/command_recorder.cc",
      "dml/command_recorder.h",
      "dml/error.h",
      "dml/platform_functions.cc",
      "dml/platform_functions.h",
    ]
  }

  deps = [
    "//base",
    "//components/ml/webnn",
    "//mojo/public/cpp/bindings",
    "//services/webnn/public/mojom",
  ]
}

source_set("tests") {
  testonly = true

  sources = [
    "webnn_context_impl_unittest.cc",
    "webnn_graph_impl_unittest.cc",
  ]

  if (is_win) {
    sources += [
      "dml/adapter_test.cc",
      "dml/command_queue_test.cc",
      "dml/command_recorder_test.cc",
      "dml/platform_functions_test.cc",
      "dml/test_base.cc",
      "dml/test_base.h",
    ]
  }

  deps = [
    ":webnn",
    "//base",
    "//base/test:test_support",
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/test_support:test_utils",
    "//services/webnn/public/mojom",
    "//testing/gtest",
  ]

  if (is_win) {
    deps += [
      "//ui/gl",
      "//ui/gl/init",
    ]
  }
}

mojolpm_fuzzer_test("webnn_graph_mojolpm_fuzzer") {
  sources = [ "webnn_graph_mojolpm_fuzzer.cc" ]

  proto_source = "webnn_graph_mojolpm_fuzzer.proto"

  proto_deps = [ "//services/webnn/public/mojom:mojom_mojolpm" ]

  deps = [
    ":webnn",
    "//base",
    "//base/test:test_support",
    "//content/test/fuzzer:mojolpm_fuzzer_support",
    "//services/webnn/public/mojom",
    "//third_party/libprotobuf-mutator",
  ]
}
