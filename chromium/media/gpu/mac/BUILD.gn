# Copyright 2019 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/features.gni")
import("//build/config/ui.gni")
import("//media/gpu/args.gni")
import("//media/media_options.gni")
import("//ui/gl/features.gni")

assert(is_apple)

import("//build/config/mac/mac_sdk.gni")

source_set("mac") {
  defines = [ "MEDIA_GPU_IMPLEMENTATION" ]
  visibility = [ "//media/gpu" ]

  configs += [ "//build/config/compiler:enable_arc" ]

  sources = [
    "video_toolbox_decompression_interface.cc",
    "video_toolbox_decompression_interface.h",
    "video_toolbox_decompression_session.cc",
    "video_toolbox_decompression_session.h",
    "video_toolbox_frame_converter.cc",
    "video_toolbox_frame_converter.h",
    "video_toolbox_h264_accelerator.cc",
    "video_toolbox_h264_accelerator.h",
    "video_toolbox_video_decoder.cc",
    "video_toolbox_video_decoder.h",
    "vp9_super_frame_bitstream_filter.cc",
    "vp9_super_frame_bitstream_filter.h",
    "vt_config_util.h",
    "vt_config_util.mm",
    "vt_video_decode_accelerator_mac.cc",
    "vt_video_decode_accelerator_mac.h",
    "vt_video_encode_accelerator_mac.cc",
    "vt_video_encode_accelerator_mac.h",
  ]
  public_deps = [ "//third_party/webrtc_overrides:webrtc_component" ]
  frameworks = [
    "CoreFoundation.framework",
    "CoreMedia.framework",
    "Foundation.framework",
    "IOSurface.framework",
    "QuartzCore.framework",
    "VideoToolbox.framework",
  ]
  deps = [
    "//base",
    "//components/crash/core/common:crash_key",
    "//components/viz/common:resource_format",
    "//gpu/command_buffer/service",
    "//gpu/command_buffer/service:gles2",
    "//gpu/ipc/service",
    "//media",
    "//media/gpu:common",
    "//ui/gfx/geometry",
    "//ui/gl",
  ]
}

source_set("unit_tests") {
  testonly = true
  frameworks = [
    "CoreFoundation.framework",
    "CoreMedia.framework",
  ]
  deps = [
    "//base/test:test_support",
    "//media:test_support",
    "//media/gpu:test_support",
    "//testing/gmock",
    "//testing/gtest",
  ]

  if (media_use_ffmpeg) {
    deps += [ "//third_party/ffmpeg" ]
  }

  sources = [
    "video_toolbox_decompression_interface_unittest.cc",
    "video_toolbox_h264_accelerator_unittest.cc",
    "vp9_super_frame_bitstream_filter_unittest.cc",
    "vt_config_util_unittest.cc",
  ]
}
