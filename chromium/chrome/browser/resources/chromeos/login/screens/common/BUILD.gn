# Copyright 2018 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/polymer/html_to_wrapper.gni")
import("../../login.gni")

assert(is_chromeos, "OOBE UI is only available on ChromeOS builds")

group("closure_compile") {
  deps = [ ":closure_compile_local" ]
}

js_type_check("closure_compile_local") {
  is_polymer3 = true
  closure_flags =
      default_closure_args + mojom_js_args + [
        "js_module_root=" +
            rebase_path("//chrome/browser/resources/gaia_auth_host/",
                        root_build_dir),
        "js_module_root=./gen/chrome/browser/resources/gaia_auth_host",
      ]
  deps = [
    ":consolidated_consent",
    ":error_message",
    ":guest_tos",
    ":offline_ad_login",
    ":recommend_apps",
    ":saml_confirm_password",
    ":signin_fatal_error",
  ]
}

###############################
# Closure compiler libraries below

js_library("consolidated_consent") {
  sources = [ "$root_gen_dir/chrome/browser/resources/chromeos/login/screens/common/consolidated_consent.js" ]
  deps = [
    "../..:display_manager",
    "../../components:display_manager_types",
    "../../components:oobe_select",
    "../../components:oobe_types",
    "../../components:web_view_helper",
    "../../components:web_view_loader",
    "../../components/behaviors:login_screen_behavior",
    "../../components/behaviors:multi_step_behavior",
    "../../components/behaviors:oobe_i18n_behavior",
    "../../components/dialogs:oobe_adaptive_dialog",
    "../../components/dialogs:oobe_loading_dialog",
    "../../components/dialogs:oobe_modal_dialog",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
  extra_deps = [ ":web_components" ]
  externs_list = [
    "$externs_path/chrome_extensions.js",
    "$externs_path/webview_tag.js",
    "//ui/webui/resources/cr_elements/cr_toggle/cr_toggle_externs.js",
  ]
}

js_library("guest_tos") {
  sources = [ "$root_gen_dir/chrome/browser/resources/chromeos/login/screens/common/guest_tos.js" ]
  deps = [
    "../..:display_manager",
    "../../components:web_view_helper",
    "../../components:web_view_loader",
    "../../components/behaviors:login_screen_behavior",
    "../../components/behaviors:multi_step_behavior",
    "../../components/behaviors:oobe_i18n_behavior",
    "../../components/dialogs:oobe_adaptive_dialog",
    "../../components/dialogs:oobe_loading_dialog",
    "../../components/dialogs:oobe_modal_dialog",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
  extra_deps = [ ":web_components" ]
  externs_list = [
    "$externs_path/chrome_extensions.js",
    "$externs_path/webview_tag.js",
    "//ui/webui/resources/cr_elements/cr_toggle/cr_toggle_externs.js",
  ]
}

js_library("offline_ad_login") {
  sources = [ "$root_gen_dir/chrome/browser/resources/chromeos/login/screens/common/offline_ad_login.js" ]
  deps = [
    "../../components:oobe_a11y_option",
    "../../components:oobe_select",
    "../../components/behaviors:login_screen_behavior",
    "../../components/behaviors:oobe_dialog_host_behavior",
    "../../components/behaviors:oobe_i18n_behavior",
    "../../components/dialogs:oobe_adaptive_dialog",
    "//ash/webui/common/resources:load_time_data.m",
  ]
  externs_list = [
    "//ui/webui/resources/cr_elements/cr_input/cr_input_externs.js",
    "//ui/webui/resources/cr_elements/cr_toggle/cr_toggle_externs.js",
  ]
  extra_deps = [ ":web_components" ]
}

# Template used by the `tools/oobe/generate_screen_template.py` script.

js_library("recommend_apps") {
  sources = [ "$root_gen_dir/chrome/browser/resources/chromeos/login/screens/common/recommend_apps.js" ]
  deps = [
    "../../components:oobe_types",
    "../../components/behaviors:login_screen_behavior",
    "../../components/behaviors:multi_step_behavior",
    "../../components/behaviors:oobe_dialog_host_behavior",
    "../../components/behaviors:oobe_i18n_behavior",
    "../../components/dialogs:oobe_adaptive_dialog",
    "../../components/dialogs:oobe_loading_dialog",
    "//ash/webui/common/resources:assert",
    "//ash/webui/common/resources:load_time_data.m",
  ]
  externs_list = [ "$externs_path/webview_tag.js" ]
  extra_deps = [ ":web_components" ]
}

js_library("saml_confirm_password") {
  sources = [ "$root_gen_dir/chrome/browser/resources/chromeos/login/screens/common/saml_confirm_password.js" ]
  deps = [
    "../../:login_ui_tools",
    "../../components/behaviors:oobe_i18n_behavior",
    "../../components/dialogs:oobe_adaptive_dialog",
    "../../components/dialogs:oobe_loading_dialog",
    "../../components/dialogs:oobe_modal_dialog",
  ]
  extra_deps = [ ":web_components" ]
}

js_library("signin_fatal_error") {
  sources = [ "$root_gen_dir/chrome/browser/resources/chromeos/login/screens/common/signin_fatal_error.js" ]
  deps = [
    "../../components:display_manager_types",
    "../../components:oobe_types",
    "../../components/behaviors:login_screen_behavior",
    "../../components/behaviors:oobe_dialog_host_behavior",
    "../../components/behaviors:oobe_i18n_behavior",
    "../../components/dialogs:oobe_adaptive_dialog",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
  extra_deps = [ ":web_components" ]
}

js_library("error_message") {
  sources = [ "$root_gen_dir/chrome/browser/resources/chromeos/login/screens/common/error_message.js" ]
  deps = [
    "../../:cr_ui",
    "../../components:display_manager_types",
    "../../components:network_select_login",
    "../../components/behaviors:login_screen_behavior",
    "../../components/behaviors:oobe_dialog_host_behavior",
    "../../components/behaviors:oobe_i18n_behavior",
    "../../components/dialogs:oobe_adaptive_dialog",
    "//ash/webui/common/resources:parse_html_subset",
  ]
  extra_deps = [ ":web_components" ]
}

group("web_components") {
  public_deps = [
    ":copy_js",
    ":generate_web_component_html_wrapper_files",
  ]
}

html_to_wrapper("generate_web_component_html_wrapper_files") {
  in_files = rebase_path(common_screens_html_files, "./screens/common", ".")
  use_js = true
}

copy("copy_js") {
  sources = rebase_path(common_screens_js_files, "./screens/common", ".")
  outputs = [ "$target_gen_dir/{{source_file_part}}" ]
}
